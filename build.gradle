buildscript {
	repositories {
		mavenLocal()
		maven { url "https://repo.grails.org/grails/core" }
	}
	dependencies {
		classpath "org.grails:grails-gradle-plugin:$grailsVersion"
		classpath "org.grails:grails-docs:$grailsVersion"
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
		classpath 'io.github.groovylang.groovydoc:groovydoc-gradle-plugin:1.0.1'
		classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.8.2"
	}
}

apply plugin: 'idea'
apply plugin:'groovy'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

//Root project and view-tools plugin uses same version.
version project.projectVersion

project(':freemarker') {
	version = project.projectVersion
}

ext {
    commonBuild = new File(project.rootDir, "gradle").absolutePath
	grailsApps = ['free-app']
	grailsPlugins = ['freemarker', 'free-plugin']
}


subprojects {

	boolean isGrailsApp = grailsApps.contains(project.name)
	boolean isGrailsPlugin = grailsPlugins.contains(project.name)
	boolean isGrailsProject = isGrailsApp || isGrailsPlugin
	boolean isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")

	println project.name + ":" + version.toString()

	ext {
		userOrg = "grails"
		set "tomcat.version", "8.5.5"
		isSnapshot = isBuildSnapshot
	}

    apply plugin:'groovy'

    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }

    dependencies {
        compile "org.springframework.boot:spring-boot-starter-logging"
        compile "org.springframework.boot:spring-boot-autoconfigure"
        compile "org.grails:grails-core"
        compile "org.grails:grails-dependencies"
        compile "org.grails:grails-web-boot"
        compile "org.grails.plugins:cache"

        runtime "com.h2database:h2"

        testCompile "org.grails:grails-gorm-testing-support"
        testCompile "org.grails:grails-plugin-testing"
        testCompile "org.grails:grails-web-testing-support"
    }

	if (isGrailsPlugin) {
        group "org.grails.plugins"

        apply plugin:"org.grails.grails-plugin"
        apply plugin:"org.grails.grails-gsp"

        dependencies {
            compile "org.springframework.boot:spring-boot-starter-actuator"
            provided "org.springframework.boot:spring-boot-starter-tomcat"

            profile "org.grails.profiles:web-plugin"
            provided "org.grails:grails-plugin-services"
            provided "org.grails:grails-plugin-domain-class"

            console "org.grails:grails-console"
        }

        apply from: "${commonBuild}/common-publishing.gradle"

	}

	if (isGrailsApp) {
		group "org.grails"

        apply plugin:"org.grails.grails-web"
        apply plugin:"war"
        apply plugin:"org.grails.grails-gsp"

        dependencies {
            profile "org.grails.profiles:web"
            compile "org.springframework.boot:spring-boot-starter-actuator"
            provided "org.springframework.boot:spring-boot-starter-tomcat"

            console "org.grails:grails-console"
        }

        bootRun {
            jvmArgs('-Dspring.output.ansi.enabled=always')
            addResources = true
        }
	}

}
